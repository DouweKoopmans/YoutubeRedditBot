buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.0'
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}

plugins {
    id 'net.ltgt.apt' version '0.10'
    id 'application'
}

repositories {
    mavenCentral()
    jcenter()
}

group 'com.fallingdutchman'
version '2.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'com.fallingdutchman.youtuberedditbot.YoutubeRedditBot'


run {
    workingDir = "run"
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.+'
    compile group: 'com.google.guava', name: 'guava', version: '25.1-jre'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4j_version
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j_version
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j_version
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
    compile group: 'org.slf4j', name: 'slf4j-ext', version: slf4j_version
    compile group: 'com.typesafe', name: 'config', version: '1.+'
    compile group: 'com.rometools', name: 'rome', version: '1.10.+'
    compile group: 'net.dean.jraw', name: 'JRAW', version: '+'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.+'
    compile group: 'com.google.apis', name:'google-api-services-youtube', version: 'v3-rev186-1.23.0'
    compile group: 'com.google.inject', name:'guice', version: '4.+'
    compile group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: '4.+'
    compile group: 'com.google.code.gson', name:'gson', version: '2.+'
    compile group: 'com.overzealous', name:'remark', version: '1.+'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.+'

    apt "org.projectlombok:lombok:1.18.+"
}

distTar {
    compression = Compression.GZIP
    extension = 'tar.gz'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3'
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

ext.priv = parseConfig(file('private.properties'))

// helper method for parsing .properties files
static ConfigObject parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

/** -- Docker -- **/

apply plugin: 'com.bmuschko.docker-java-application'

def imageName = 'douwekoopmans/youtuberedditbot'

// general configurations
docker {
    javaApplication {
        baseImage = 'java'
        maintainer = 'Douwe Koopmans'
        ports = [8080]
        tag = 'douwekoopmans/youtuberedditbot'
    }

    registryCredentials {
        url = 'https://index.docker.io/v1'
        username = priv.dockerUser
        password = priv.dockerPass
        email = priv.dockerEmail
    }
    url = 'unix:///var/run/docker.sock'
}


import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

// this is where the docker image will be build from, so this has to include all the config and
def tempDir = project.file('build/mydocker/')

task copyDockerResources(type: Copy) {
    dependsOn distTar
    from { distTar.archivePath }

    into { tempDir }
}

task createDockerfile(type: Dockerfile) {
    dependsOn copyDockerResources
    destFile = new File("$tempDir/Dockerfile")

    from docker.javaApplication.baseImage
    maintainer docker.javaApplication.maintainer
    addFile "$distTar.archivePath.name", '/'
    entryPoint({
        def installDir = distTar.archiveName - ".$distTar.extension"
        "/$installDir/bin/$project.applicationName".toString() })
    exposePort { docker.javaApplication.ports }
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = tempDir
    tag = docker.javaApplication.tag
}

task tagImage(type: DockerTagImage) {
    dependsOn buildImage
    imageId = "$imageName:$docker.javaApplication.tag"
    tag = 'latest'
    repository = "$imageName"
}

dockerPushImage.imageName = imageName
dockerPushImage.dependsOn tagImage

//task pushImage(type: DockerPushImage) {
//    dependsOn tagImage
//    imageName = "$docker.javaApplication.tag"
//    tag = 'latest'
//}